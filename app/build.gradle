apply plugin: 'com.android.application'

android {
    compileSdkVersion toolVersions.android.compileSdk
    buildToolsVersion toolVersions.android.buildTools

    defaultConfig {
        applicationId "com.folen.androidshowreel"
        minSdkVersion toolVersions.android.minSdk
        targetSdkVersion toolVersions.android.targetSdk
        versionCode projectVersions.versionCode.toInteger()
        versionName projectVersions.versionName
        multiDexEnabled true
        vectorDrawables.useSupportLibrary = true
    }

    buildTypes {
        release {
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            debuggable false
        }
        developer {
            initWith(release)
            applicationIdSuffix ".dev"
            debuggable true
        }
    }

    packagingOptions {
        exclude 'META-INF/androidx.core_core.version'
        exclude 'META-INF/androidx.versionedparcelable_versionedparcelable.version'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    configurations.all {
        resolutionStrategy {
            eachDependency { details ->
                // Force all of the primary support libraries to use the same version.
                if (details.requested.group == 'com.android.support') {
                    details.useVersion libVersions.support
                }
            }
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation "com.android.support:appcompat-v7:$libVersions.support"
    implementation "com.android.support.constraint:constraint-layout:$libVersions.constraint"
    implementation "com.android.support:design:$libVersions.support"
    implementation "com.android.support:cardview-v7:$libVersions.support"

    implementation "com.mikepenz:fastadapter-commons:$libVersions.fastAdapter"
    implementation "com.google.code.gson:gson:$libVersions.gson"
    implementation "com.github.bumptech.glide:glide:$libVersions.glide"
    annotationProcessor "com.github.bumptech.glide:compiler:$libVersions.glide"
}
